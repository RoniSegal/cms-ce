
allprojects {
	group = 'com.enonic.cms'
	buildNumber = new Date().format("yyyyMMdd.HHmmss")
    scriptDir = "$rootDir/gradle"
}

apply from: "$scriptDir/wrapper.gradle"
apply from: "$scriptDir/idea.gradle"

versions = [
 	servlet:  '2.5',
    hibernate: '3.5.6-Final',
    spring: '3.0.5.RELEASE',
    spring_osgi: '1.2.1',
    slf4j: '1.6.1',
    saxon: '9.1.0.8',
    jackrabbit: '2.2.4',
    junit: '4.8.2'
]

allprojects {
	repositories {
		mavenCentral()
		mavenRepo urls: 'http://repo.enonic.com/maven'
	}
}

javaProjects = subprojects.findAll { project ->
	project.path.startsWith(':cms-')
}

warProjects = subprojects.findAll { project ->
	project.path.equals(':cms-webapp')
}

publishProjects = subprojects.findAll { project ->
	!project.path.equals(':cms-itest')
}

configure(javaProjects) {
    apply from: "$scriptDir/java.gradle"
    apply from: "$scriptDir/idea.gradle"
}

configure(warProjects) {
    apply from: "$scriptDir/war.gradle"
}

configure(publishProjects) {
    apply from: "$scriptDir/publish.gradle"
}
